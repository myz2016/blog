# 异常

## 单个catch块

## 源码

```java
public class Demo14 {
    public static void main(String[] args) {
        int i = 0;
        try {
            i = 10;
        } catch (Exception e) {
            i = 20;
        }
    }
}
```



## 反编译后的字节码

只显示需要的字节码

```java
 public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=3, args_size=1
         0: iconst_0
         1: istore_1
         2: bipush        10
         4: istore_1
         5: goto          12
         8: astore_2
         9: bipush        20
        11: istore_1
        12: return
      Exception table:
         from    to  target type
             2     5     8   Class java/lang/Exception
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            9       3     2     e   Ljava/lang/Exception;
            0      13     0  args   [Ljava/lang/String;
            2      11     1     i   I
```

### iconst_0

将0压入操作数栈

### istore_1

将0存储在局部变量表1号槽位

### bipush   10

将10压入操作数栈

### istore_1

将10存储在局部变量表1号槽位（之前的0被覆盖了）

### goto   12

跳转到12行，即 `return`，程序结束

以上执行顺序是没有发生异常的情况

## Exception table 异常表

| from | to   | target | type                      |
| ---- | ---- | ------ | ------------------------- |
| 2    | 5    | 8      | Class java/lang/Exception |

[from, to) 是前闭后开的检测范围，一旦这个范围内的字节码执行出现异常，则通过 type 匹配异常类型，如果一致，进入 target 所指示行号。

从反编译的字节的第2行开始就进入了源码中`try`块的范围，第8行到第11行对应源码中catch块的内容。

如果第2行到第5行（不包括第5行），也就是第2行到第4行出现了异常，并且异常与异常表中声明的异常一致，或者是声明异常的子类异常，就会跳转到第8行，即`astore_2`。

### astore_2

将异常对象存储在局部变量表2号槽位

### bipush   20

将20压入操作数栈

### istore_1

将20存储在局部变量表1号槽位（之前的10被覆盖了）

### return

方法结束

## 多个catch块

## 源码

```java
public class Demo15 {
    public static void main(String[] args) {
        int i = 0;
        try {
            i = 10;
        } catch (ArithmeticException e) {
            i = 30;
        } catch (NullPointerException e) {
            i = 40;
        } catch (Exception e) {
            i = 50;
        }
    }
}
```

反编译后的字节码

```java
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=3, args_size=1
         0: iconst_0
         1: istore_1
         2: bipush        10
         4: istore_1
         5: goto          26
         8: astore_2
         9: bipush        30
        11: istore_1
        12: goto          26
        15: astore_2
        16: bipush        40
        18: istore_1
        19: goto          26
        22: astore_2
        23: bipush        50
        25: istore_1
        26: return
      Exception table:
         from    to  target type
             2     5     8   Class java/lang/ArithmeticException
             2     5    15   Class java/lang/NullPointerException
             2     5    22   Class java/lang/Exception
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            9       3     2     e   Ljava/lang/ArithmeticException;
           16       3     2     e   Ljava/lang/NullPointerException;
           23       3     2     e   Ljava/lang/Exception;
            0      27     0  args   [Ljava/lang/String;
            2      25     1     i   I

```

由异常表得知

* 当执行2~4行代码发生异常，并且是`ArithmeticException`异常或其之类异常时，会跳转到第8行
* 当执行2~4行代码发生异常，并且是`NullPointerException`异常或其之类异常时，会跳转到第15行
* 当执行2~4行代码发生异常，并且是Exception异常或其之类异常时，会跳转到第22行

还有一点需要注意的是，3种异常都会存在于局部变量表2号槽位（因为异常出现时，只能进入 Exception table 中一个分支，所以局部变量表 slot 2 位置被共用），这也是虚拟机的一种优化，使得槽位得以复用。

## multi-catch的情况

## 源码

```java
public class Demo16 {
    public static void main(String[] args) {
        try {
            final Method test = Demo16.class.getMethod("test");
            test.invoke(null);
        } catch (NoSuchMethodException | IllegalAccessException | InvocationTargetException e) {
            e.printStackTrace();
        }
    }
}
```

## 反编译后的字节码

```java
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=2, args_size=1
         0: ldc           #2                  // class bytecode/Demo16
         2: ldc           #3                  // String test
         4: iconst_0
         5: anewarray     #4                  // class java/lang/Class
         8: invokevirtual #5                  // Method java/lang/Class.getMethod:(Ljava/lang/String;[Ljava/lang/Class;)Ljava/lang/reflect/Method;
        11: astore_1
        12: aload_1
        13: aconst_null
        14: iconst_0
        15: anewarray     #6                  // class java/lang/Object
        18: invokevirtual #7                  // Method java/lang/reflect/Method.invoke:(Ljava/lang/Object;[Ljava/lang/Object;)Ljava/lang/Object;
        21: pop
        22: goto          30
        25: astore_1
        26: aload_1
        27: invokevirtual #11                 // Method java/lang/ReflectiveOperationException.printStackTrace:()V
        30: return
      Exception table:
         from    to  target type
             0    22    25   Class java/lang/NoSuchMethodException
             0    22    25   Class java/lang/IllegalAccessException
             0    22    25   Class java/lang/reflect/InvocationTargetException
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
           12      10     1  test   Ljava/lang/reflect/Method;
           26       4     1     e   Ljava/lang/ReflectiveOperationException;
            0      31     0  args   [Ljava/lang/String;
```

这里主要分析`catch`块部分字节码。

catch多个异常是`jdk1.7`的新语法。`Exception table`可以可看出，当0~22行代码了异常，无论是`NoSuchMethodException`、`IllegalAccessException`还是`InvocationTargetException`，都会跳转到第25行。它会将异常对象的引用存储在局部变量表1号槽位。

### aload_1

加载1号槽位的异常对象的引用

### invokevirtual #11

调用`printStackTrace()`方法，对应源码：`e.printStackTrace()`

## 异常-finally

##  源码

```java
public class Demo17 {
    public static void main(String[] args) {
        int i = 0;
        try {
            i = 10;
        } catch (Exception e) {
            i = 20;
        } finally {
            i = 30;
        }
    }
}
```

## 反编译后的字节码

```java
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=4, args_size=1
         0: iconst_0
         1: istore_1				// 0 -> i
         2: bipush        10		 // try --------------------------------------
         4: istore_1				// 10 -> i								  |
         5: bipush        30		 // finally								   |
         7: istore_1                  // 30 -> i							    |
         8: goto          27          // return -----------------------------------
        11: astore_2				// catch Exceptin -> e -----------------------
        12: bipush        20		 //										   |
        14: istore_1				// 20 -> i								   |
        15: bipush        30         // finally                                     |
        17: istore_1			    // 30 -> i								    |
        18: goto          27         // return -------------------------------------
        21: astore_3                 // catch any -> slot 3 ------------------------
        22: bipush        30         // finally                                     |
        24: istore_1			    // i = 30								    |
        25: aload_3                  // <- slot 3                                   |
        26: athrow                   // throw ---------------------------------------
        27: return
      Exception table:
         from    to  target type
             2     5    11   Class java/lang/Exception
             2     5    21   any	// 剩余的异常类型，比如 Error
            11    15    21   any	// 剩余的异常类型，比如 Error
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
           12       3     2     e   Ljava/lang/Exception;
            0      28     0  args   [Ljava/lang/String;
            2      26     1     i   I
```

可以看到 finally 中的代码被复制了 3 份，分别放入 try 流程，catch 流程以及 catch 剩余的异常类型流
程。这也就保证了，finally块中的代码，最终会被执行。

注意看异常表的2~3行

`try`块中，如果发生了`Exception`解决不了的异常，比如`Error`、`Throwable`，这时就会跳转到第`21`行，然后将异常对象引用存入局部变量表`3`号槽位，然后执行`finally`块代码，将`30`赋值给`i`（确保finally块中的代码一定会被执行），然后加载异常对象引用，最后使用`athrow`指令将异常抛出；当`catch`块中发生了异常，也会跳转到第`21`行，然后执行同样的finally操作和异常抛出。

slot 3 我们在这里看不到，其实是用`slot 3`槽位的，但是它没有名字。由`locals=4`得知，局部变量表长度为4，所以`slot 3`虽然没有显示，但是确实是存在的。

以上的代码一共有三个分支

1. try分支
2. catch分支
3. catch分支没有匹配到的异常的分支

finally代码会被复制到这三个分支，三个分支各存一份，确保finally块代码一定被执行。

## 练习1

### 下面的代码会输出什么结果？

## 源码

```java
public class Demo18 {
    public static void main(String[] args) {
        int test = test();
        System.out.println(test); // 20
    }
    public static int test() {
        try {
            return 10;
        } finally {
            return 20;
        }
    }
}
```

## 反编译后的字节码

```java
 public static int test();
    descriptor: ()I
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=2, args_size=0
         0: bipush        10	// <- 10 压入操作数栈
         2: istore_0		   // 10 -> slot 0 (从操作数栈移除了)
         3: bipush        20    // finally <- 20 压入操作数栈
         5: ireturn             // 返回操作数栈顶 20
         6: astore_1            // catch any -> slot 1
         7: bipush        20    // <- 20 压入操作数栈
         9: ireturn             // 返回操作数栈顶 20
      Exception table:
         from    to  target type
             0     3     6   any
```

`0~2`行如果发生了异常，就会跳转到`6`行，然后执行`finally`块代码，最终返回`20`

* 由于 finally 中的`ireturn`被插入了所有可能的流程，因此返回结果肯定以 finally 的为准
* 跟上例中的 finally 相比，发现没有`athrow`了，这告诉我们：**如果在 finally 中出现了 return，会**
  **吞掉异常，导致即使发生了异常，你也不知道**。

以下代码演示此问题

```java
public class Demo19 {
    public static void main(String[] args) {
        int test = test();
        System.out.println(test);
    }
    public static int test() {
        try {
            int i = 1 / 0;
            return 10;
        } finally {
            return 20;
        }
    }
}
```

以上代码在`int i = 1 / 0;`会出现异常，但由于`finally`块中使用了`return`，所以方法正常结束，返回20，并没有抛出任何异常，但**实际上是存在异常的！**

## 伏笔

`2: istore_0`

以上的操作貌似是没有任何意义的，那么为什么将 10 存入局部变量表0号槽位呢？下面会给出答案。

## 练习2

下面的代码会输出什么？

```java
public class Demo20 {
    public static void main(String[] args) {
        int result = test();
        System.out.println(result); // 10
    }

    public static int test() {
        int i = 10;
        try {
            return i;
        } finally {
            i = 20;
        }
    }
}
```

## 源码

```java
public class Demo20 {
    public static void main(String[] args) {
        int result = test();
        System.out.println(result);
    }

    public static int test() {
        int i = 10;
        try {
            return i;
        } finally {
            i = 20;
        }
    }
}
```



## 反编译后的字节码

```java
public static int test();
    descriptor: ()I
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=3, args_size=0
         0: bipush        10  			 // <- 10 放入栈顶
         2: istore_0				    // 10 -> i
         3: iload_0						// <- i(10)
         4: istore_1					// 10 -> slot 1，暂存至 slot 1，目的是为了固定返回值
         5: bipush        20			 // <- 20 放入栈顶
         7: istore_0					// 20 -> i
         8: iload_1						// <- slot 1(10) 载入 slot 1 暂存的值
         9: ireturn						// 返回栈顶的 int(10)
        10: astore_2
        11: bipush        20
        13: istore_0
        14: aload_2
        15: athrow
      Exception table:
         from    to  target type
             3     5    10   any
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            3      13     0     i   I
```

### bipush   10

将10压入操作数栈

### istore_0

将10存入局部变量表0号槽位

### iload_0

将局部变量表0号槽位的数据压入操作数栈

### istore_1

将栈顶数据存入局部变量表1号槽位，1号槽位是没有名字的。

这也就是上面留下的伏笔，目的就是为了固定返回值。

###  bipush    20

执行finally块代码，将20压入操作数栈

###  istore_0

执行finally块，将20存储到局部变量表0号槽位，此时`finally`块的代码就执行完了

### iload_1

将局部变量表1号槽位的刚暂存的值压入操作数栈

### ireturn

将10返回，所以最终返回结果是10

如果在3~4行出现异常，就会跳转到第10行

### astore_2

存储异常对象引用到局部变量表2号槽位

### bipush   20

执行finally块，将20压入操作数栈

###  istore_0

将20存储到局部变量表0号槽位，此时`finally`块的代码就执行完了

### aload_2

加载异常对象引用

### athrow

抛出异常

`finally`块中不带`return`语句，如果发生异常，会抛出异常，不会吞掉异常。

在`try`中如果`return`了，即使在`finally`块中对变量重新赋值，也是没有用的。通过字节码得知，它在`return`之前，先做了一个暂存，然后执行`finally`中的代码，然后再将暂存的值恢复到栈顶，最后返回，所以返回的还是执行`return`时暂存的值。